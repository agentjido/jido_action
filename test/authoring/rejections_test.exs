Code.require_file("support/components.ex", __DIR__)
Code.require_file("support/hostile.ex", __DIR__)

defmodule JidoActionTest.Authoring.RejectionsTest do
  use ExUnit.Case, async: false
  @moduletag :authoring

  alias Jido.{Exec, Flow}
  alias Jido.Flow.{Definition, Graph, Ref}
  alias JidoActionTest.Authoring.Components
  alias JidoActionTest.Authoring.Hostile

  test "Action-only component slots reject a Flow target at executable validation" do
    child = Components.Child

    option =
      JidoActionTest.FlowComponent.option!(name: "selected", condition: true, action: child)

    fallback = JidoActionTest.FlowComponent.fallback!(action: Components.Echo)

    state =
      Components.IterateRepeat.flow().components |> Map.fetch!("counter") |> Map.fetch!(:state)

    components = [
      {JidoActionTest.FlowComponent.map!(name: "node", collection: [], action: child), :action},
      {JidoActionTest.FlowComponent.reduce!(
         name: "node",
         collection: [],
         initial: %{},
         action: child
       ), :action},
      {JidoActionTest.FlowComponent.iterate!(
         name: "node",
         state: state,
         action: child,
         completion: true,
         max_iterations: 1
       ), :action},
      {JidoActionTest.FlowComponent.choice!(name: "node", options: [option], fallback: fallback),
       "selected"},
      {JidoActionTest.FlowComponent.choice!(
         name: "node",
         options: [
           JidoActionTest.FlowComponent.option!(
             name: "selected",
             condition: true,
             action: Components.Echo
           )
         ],
         fallback: JidoActionTest.FlowComponent.fallback!(action: child)
       ), :fallback},
      {JidoActionTest.FlowComponent.dispatch!(
         name: "node",
         decision: child,
         expander: Components.Expand
       ), :decision},
      {JidoActionTest.FlowComponent.dispatch!(
         name: "node",
         decision: Components.Decide,
         expander: child
       ), :expander}
    ]

    for {component, field} <- components do
      flow =
        JidoActionTest.FlowBuilder.new!(
          name: "wrong_target",
          components: [component],
          output: Ref.result("node")
        )

      assert {:error, error} = Jido.Exec.Compiler.validate(flow)
      assert error.details.component == "node"
      assert error.details.field == field
      assert error.details.expected == :action
      assert error.details.actual == :flow
      assert {:error, _} = Exec.run(flow)
    end
  end

  test "Map, Reduce, and Iterate reject references outside each local scope" do
    assert {:error, map_error} =
             JidoActionTest.FlowComponent.map(
               name: "map",
               collection: Ref.item(),
               action: Components.MapItem
             )

    assert map_error.details.ref_type == :item
    assert map_error.details.scope == :map_collection

    assert {:error, map_params_error} =
             JidoActionTest.FlowComponent.map(
               name: "map",
               collection: [],
               action: Components.MapItem,
               params: %{value: Ref.accumulator()}
             )

    assert map_params_error.details.ref_type == :accumulator
    assert map_params_error.details.scope == :map_params

    assert {:error, reduce_error} =
             JidoActionTest.FlowComponent.reduce(
               name: "reduce",
               collection: [],
               initial: Ref.accumulator(),
               action: Components.Fold
             )

    assert reduce_error.details.ref_type == :accumulator
    assert reduce_error.details.scope == :reduce_initial

    assert {:error, iterate_error} =
             JidoActionTest.FlowComponent.state(initial: Ref.state(:count), update: %{})

    assert iterate_error.details.ref_type == :state
    assert iterate_error.details.scope == :iterate_initial
  end

  test "Reduce rejects an invalid initial value before running its body" do
    assert {:error, error} =
             Exec.run(
               Hostile.ReduceInitial,
               %{items: [1, 2], initial: :not_a_map},
               %{observer: self()}
             )

    assert error.message == "reduce initial value must be a map or Jido.Action.Output"
    assert error.details.phase == :reduce_initial
    assert error.details.node == "fold"
    refute_received {:fold, _, _}
  end

  test "Choice rejects missing fallback and non-Boolean direct conditions" do
    valid_option =
      JidoActionTest.FlowComponent.option!(name: "ready", condition: true, action: Hostile.Watch)

    assert {:error, missing} =
             JidoActionTest.FlowComponent.choice(name: "route", options: [valid_option])

    assert missing.message =~ "fallback"

    assert {:error, condition} =
             JidoActionTest.FlowComponent.option(
               name: "ready",
               condition: :truthy,
               action: Hostile.Watch
             )

    assert condition.message =~ "condition"
    refute_received {:hostile_action, _}
  end

  test "a non-Boolean Choice condition never calls either target" do
    assert {:error, error} =
             Exec.run(Hostile.ChoiceNonBoolean, %{condition: "yes"}, %{observer: self()})

    assert error.details.node == "route"
    assert error.details.phase == :choice_condition
    assert error.details.reason == :invalid_boolean_operand
    refute_received {:hostile_action, _}

    assert Exec.run(Hostile.ChoiceNonBoolean, %{condition: true}) ==
             {:ok, %{branch: :selected}}

    assert Exec.run(Hostile.ChoiceNonBoolean, %{condition: false}) ==
             {:ok, %{branch: :fallback}}
  end

  test "Iterate validates initial and replacement State before another body call" do
    assert {:error, initial_error} =
             Exec.run(Hostile.IterateInitial, %{seed: "wrong type"}, %{observer: self()})

    assert initial_error.details.node == "counter"
    assert initial_error.details.phase == :iterate_state_initial
    refute_received {:hostile_action, _}

    assert {:error, replacement_error} =
             Exec.run(Hostile.IterateReplacement, %{}, %{observer: self()})

    assert replacement_error.details.node == "counter"
    assert replacement_error.details.phase == :iterate_state_update
    assert_receive :bad_state_called
    refute_received :bad_state_called
  end

  test "Iterate rejects a non-Boolean completion value before its body" do
    assert {:error, error} =
             Exec.run(Hostile.IterateCondition, %{condition: "truthy"}, %{observer: self()})

    assert error.details.node == "counter"
    refute_received {:hostile_action, _}
  end

  test "Iterate source rejects a missing while bound and a Flow body target" do
    for {suffix, action, bound, message} <- [
          {"MissingBound", Hostile.Watch, "", "iterate max_iterations must be an integer"},
          {"FlowTarget", Hostile.ValidatedFinalFlow, "max_iterations 2",
           "Flow component has the wrong target kind"}
        ] do
      source = """
      defmodule JidoActionTest.Authoring.Hostile.#{suffix} do
        use Jido.Flow, name: "authoring_iterate_#{suffix}"
        flow do
          iterate "counter" do
            state Zoi.object(%{count: Zoi.integer()}), initial: %{count: 0}
            action #{inspect(action)}
            params %{count: state(:count)}
            update %{count: body_result(:count)}
            while state(:count) < 1
            #{bound}
          end
          output result("counter")
        end
      end
      """

      file = "authoring_iterate_#{suffix}.ex"
      error = assert_raise CompileError, fn -> Code.compile_string(source, file) end
      assert error.file == file
      assert Exception.message(error) =~ message
    end
  end

  test "Dispatch rejects a second sink, a second Dispatch, and a partial output" do
    dispatch = definition(Components.DispatchFlow, "route")

    tail =
      JidoActionTest.FlowComponent.step!(name: "tail", action: Components.Echo, needs: ["route"])

    second =
      JidoActionTest.FlowComponent.dispatch!(
        name: "again",
        decision: Components.Decide,
        expander: Components.Expand
      )

    cases = [
      {[dispatch, tail], Ref.result("tail"), "Dispatch must be the final component"},
      {[dispatch, second], Ref.result("route"), "only one Dispatch"},
      {[dispatch], %{value: Ref.result("route", :value)}, "complete Dispatch result"}
    ]

    for {components, output, message} <- cases do
      assert {:error, error} =
               JidoActionTest.FlowBuilder.new(
                 name: "invalid_dispatch",
                 components: components,
                 output: output
               )

      assert error.message =~ message
    end

    parent =
      JidoActionTest.FlowBuilder.new!(
        name: "dispatch_parent",
        components: [
          JidoActionTest.FlowComponent.subflow!(name: "child", flow: Components.DispatchFlow)
        ],
        output: Ref.result("child")
      )

    assert {:ok, ^parent} = Jido.Exec.Compiler.validate(parent)
  end

  test "a wrong-kind source declaration reports its own source file and line" do
    source = """
    defmodule JidoActionTest.Authoring.Hostile.WrongKindSource do
      use Jido.Flow, name: "wrong_kind_source"
      flow do
        map "items", collection: input(:items), action: JidoActionTest.Authoring.Components.Child, params: %{}
        output %{items: result("items")}
      end
    end
    """

    error =
      assert_raise CompileError, fn ->
        Code.compile_string(source, "authoring_wrong_kind_source.ex")
      end

    assert error.file == "authoring_wrong_kind_source.ex"
    assert error.line == 4
    assert Exception.message(error) =~ "Flow component has the wrong target kind"
  end

  test "only references and needs order a diamond declared in reverse source order" do
    flow = Hostile.Diamond.flow()
    assert flow.components |> Map.keys() |> Enum.sort() == ["left", "right", "root", "sink"]

    assert flow.components |> Graph.canonical_components() |> Enum.map(&elem(&1, 0)) ==
             ["root", "left", "right", "sink"]

    assert {:ok, dependencies} = Flow.dependencies(flow)
    assert dependencies["root"].effective == []
    assert dependencies["left"] == %{references: [], needs: ["root"], effective: ["root"]}
    assert dependencies["right"] == %{references: ["root"], needs: [], effective: ["root"]}

    assert dependencies["sink"] == %{
             references: ["right"],
             needs: ["left"],
             effective: ["left", "right"]
           }

    assert Exec.run(Hostile.Diamond, %{value: 8}, %{observer: self()}, max_concurrency: 2) ==
             {:ok, %{value: 8}}

    assert_receive {:hostile_action, %{id: "root", value: 8}}
    assert_receive {:hostile_action, %{id: first, value: 8}}
    assert_receive {:hostile_action, %{id: second, value: 8}}
    assert MapSet.new([first, second]) == MapSet.new(["left", "right"])
    assert_receive {:hostile_action, %{id: "sink", value: 8}}
    refute_received {:hostile_action, _}
  end

  test "Dispatch and ordinary Steps reject a decision or Step continuation" do
    dispatch =
      JidoActionTest.FlowComponent.dispatch!(
        name: "route",
        decision: Hostile.Continues,
        expander: Hostile.Watch,
        params: %{value: Ref.input(:value)}
      )

    dispatch_flow =
      JidoActionTest.FlowBuilder.new!(
        name: "illegal_decision",
        components: [dispatch],
        output: Ref.result("route")
      )

    step_flow =
      JidoActionTest.FlowBuilder.new!(
        name: "illegal_step",
        components: [
          JidoActionTest.FlowComponent.step!(
            name: "early",
            action: Hostile.Continues,
            params: %{value: Ref.input(:value)}
          )
        ],
        output: Ref.result("early")
      )

    for flow <- [dispatch_flow, step_flow] do
      assert {:error, error} = Exec.run(flow, %{value: 1}, %{observer: self()})
      assert error.message == "Action continuations are not supported by Runic execution"
      assert error.details.reason == :unsupported_continuation
      assert_receive :attempted_continuation
      refute_received {:hostile_action, _}
    end
  end

  test "Dispatch targets and their parent Flow validate their outputs" do
    dispatch = definition(Components.DispatchFlow, "route")

    strict_root =
      JidoActionTest.FlowBuilder.new!(
        name: "strict_dispatch_root",
        components: [dispatch],
        output: Ref.result("route"),
        output_schema: Zoi.object(%{value: Zoi.string()})
      )

    context = %{label: "shared"}

    assert {:error, _root_schema_error} =
             Exec.run(strict_root, %{mode: :finish, value: 5, target: nil}, context)

    permissive_root = %{strict_root | output_schema: []}

    for target <- [Hostile.ValidatedFinal, Hostile.ValidatedFinalFlow] do
      input = %{mode: :continue, value: 5, target: target}
      assert {:error, root_schema_error} = Exec.run(strict_root, input, context)
      assert root_schema_error.details.phase == :flow_output

      assert Exec.run(permissive_root, input, context) ==
               {:ok, %{value: 5, label: "shared"}}

      assert {:error, direct_error} = Exec.run(target, %{value: "bad"}, context)

      assert {:error, continued_error} =
               Exec.run(permissive_root, %{input | value: "bad"}, context)

      assert continued_error.__struct__ == direct_error.__struct__
      assert continued_error.message == direct_error.message
    end
  end

  test "a Dispatch target cannot start an Action continuation chain" do
    assert {:error, error} =
             Exec.run(
               Components.DispatchFlow,
               %{mode: :continue, value: 5, target: Hostile.Loop},
               %{observer: self()}
             )

    assert error.message == "Action continuations are not supported by Runic execution"
    assert error.details.reason == :unsupported_continuation
    assert_receive {:loop_started, 5}
    refute_received {:loop_started, _}
  end

  defp definition(module, name) do
    flow = module.flow()
    Definition.component_to_definition({name, Map.fetch!(flow.components, name)})
  end
end
