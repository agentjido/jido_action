defmodule Jido.Exec.Runner.ManagedTest do
  use ExUnit.Case, async: false

  alias Jido.Action.Error.{ExecutionFailureError, TimeoutError}
  alias Jido.Exec
  alias Jido.Flow
  alias Jido.Flow.Ref
  alias Jido.Instruction

  defmodule Gate do
    use GenServer

    def start_link(owner), do: GenServer.start_link(__MODULE__, owner, name: __MODULE__)
    def wait, do: GenServer.call(__MODULE__, :wait, :infinity)
    def open, do: GenServer.call(__MODULE__, :open)

    @impl GenServer
    def init(owner), do: {:ok, %{owner: owner, open?: false, waiting: []}}

    @impl GenServer
    def handle_call(:wait, from, %{open?: false} = state) do
      send(state.owner, :gate_waiting)
      {:noreply, %{state | waiting: [from | state.waiting]}}
    end

    def handle_call(:wait, _from, state), do: {:reply, :ok, state}

    def handle_call(:open, _from, state) do
      Enum.each(state.waiting, &GenServer.reply(&1, :ok))
      {:reply, :ok, %{state | open?: true, waiting: []}}
    end
  end

  defmodule First do
    use Jido.Action, name: "managed_first"

    @impl true
    def run(_params, _context), do: {:ok, %{value: 1}, [:first]}
  end

  defmodule Second do
    use Jido.Action, name: "managed_second"

    @impl true
    def run(%{value: value}, context) do
      :ok = Gate.wait()
      {:ok, %{value: value + 1, request_id: context.request_id}, [:second]}
    end
  end

  defmodule Echo do
    use Jido.Action, name: "managed_echo"

    @impl true
    def run(params, context), do: {:ok, Map.put(params, :seen, Map.has_key?(context, :api_key))}
  end

  defmodule Blocks do
    use Jido.Action, name: "managed_blocks"

    @impl true
    def run(_params, _context) do
      receive do
        :never -> {:ok, %{}}
      end
    end
  end

  defmodule Kills do
    use Jido.Action, name: "managed_kills"

    @impl true
    def run(_params, _context), do: Process.exit(self(), :kill)
  end

  test "resume/4 restores context, policy, and the managed result" do
    runner = start_runner!(__MODULE__.ResumeRunner)
    start_supervised!({Gate, self()})
    id = {:resume, System.unique_integer([:positive])}
    context = %{request_id: "r-42"}
    opts = [timeout: 60_000, max_attempts: 3, max_concurrency: 1]

    assert {:ok, _worker} =
             Exec.start(
               runner,
               id,
               two_step_flow(),
               %{},
               context,
               opts ++ [checkpoint_strategy: :every_cycle]
             )

    assert_receive :gate_waiting, 2_000
    assert :ok = Runic.Runner.checkpoint(runner, id)
    assert :ok = Runic.Runner.stop(runner, id, persist: true)
    :ok = Gate.open()

    test_pid = self()
    idle = [on_idle: fn _state -> send(test_pid, {:idle, id}) end]
    assert {:ok, _worker} = Exec.resume(runner, id, context, opts ++ [hooks: idle])
    assert_receive {:idle, ^id}, 2_000

    assert {:ok, workflow} = Runic.Runner.get_workflow(runner, id)
    assert [default: policy] = workflow.scheduler_policies
    assert %{timeout_ms: 60_000, max_retries: 2, execution_mode: :durable} = policy

    assert Exec.result(workflow) ==
             {:ok, %{value: 2, request_id: "r-42"}, [:first, :second]}
  end

  test "managed Action targets keep context out of the Runic store" do
    runner = start_runner!(__MODULE__.SecretRunner)
    id = {:secret, System.unique_integer([:positive])}

    assert {:ok, _worker} =
             Exec.start(runner, id, Echo, %{value: 1}, %{api_key: "SECRET-KEY-123"},
               hooks: idle_hook(id)
             )

    assert_receive {:idle, ^id}, 2_000
    assert {:ok, workflow} = Runic.Runner.get_workflow(runner, id)
    assert Exec.result(workflow) == {:ok, %{value: 1, seen: true}}

    {store, store_state} = Runic.Runner.get_store(runner)
    assert {:ok, events} = store.stream(id, store_state)
    refute inspect(Enum.to_list(events), limit: :infinity) =~ "SECRET-KEY-123"
  end

  test "managed execution rejects non-portable Instruction metadata" do
    runner = start_runner!(__MODULE__.MetadataRunner)
    instruction = Instruction.new!(target: Echo, metadata: %{owner: self()})

    assert {:error, %ExecutionFailureError{details: details}} =
             Exec.start(runner, {:metadata, 1}, instruction)

    assert %{reason: :non_portable_durable_value, path: [:metadata, :owner]} = details
  end

  test "start/6 rejects invalid managed option values" do
    runner = start_runner!(__MODULE__.OptionRunner)

    for {key, value} <- [
          max_concurrency: 0,
          max_concurrency: :foo,
          dispatch_mode: :bogus,
          checkpoint_strategy: :bogus,
          checkpoint_strategy: {:every_n, 0},
          hooks: :bad,
          executor: NoSuchExecutor
        ] do
      assert {:error, %Jido.Action.Error.ConfigurationError{details: details}} =
               Exec.start(runner, {:options, key}, Echo, %{}, %{}, [{key, value}])

      assert details == %{option: key, value: value}
    end

    assert {:error, :not_found} = Runic.Runner.get_workflow(runner, {:options, :max_concurrency})
  end

  test "the default executor uses a partitioned Runner Task Supervisor" do
    runner = start_runner!(__MODULE__.PartitionRunner, task_supervisor: {:partition, 2})
    id = {:partition, System.unique_integer([:positive])}

    assert {:ok, _worker} = Exec.start(runner, id, Echo, %{value: 3}, %{}, hooks: idle_hook(id))
    assert_receive {:idle, ^id}, 2_000
    assert {:ok, workflow} = Runic.Runner.get_workflow(runner, id)
    assert Exec.result(workflow) == {:ok, %{value: 3, seen: false}}
  end

  test "result/1 projects managed failures to public Jido errors" do
    runner = start_runner!(__MODULE__.FailureRunner)

    for {target, opts, error} <- [
          {Blocks, [timeout: 20], TimeoutError},
          {Kills, [], ExecutionFailureError}
        ] do
      id = {:failure, System.unique_integer([:positive])}

      assert {:ok, _worker} =
               Exec.start(runner, id, target, %{}, %{}, opts ++ [hooks: idle_hook(id)])

      assert_receive {:idle, ^id}, 2_000
      assert {:ok, workflow} = Runic.Runner.get_workflow(runner, id)
      assert {:error, %^error{}} = Exec.result(workflow)
    end
  end

  defp start_runner!(name, opts \\ []) do
    start_supervised!({Runic.Runner, Keyword.put(opts, :name, name)})
    name
  end

  defp idle_hook(id) do
    test_pid = self()
    [on_idle: fn _state -> send(test_pid, {:idle, id}) end]
  end

  defp two_step_flow do
    Flow.new!(%{
      name: "managed_two_step",
      components: [
        %{kind: :step, name: "first", action: First, params: %{}},
        %{
          kind: :step,
          name: "second",
          action: Second,
          params: %{value: Ref.result("first", :value)}
        }
      ],
      output: Ref.result("second")
    })
  end
end
